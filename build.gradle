buildscript {
    // 定义方法遍历子项目
    ext {
        springBootVersion = '3.3.1' // Update to your current version
        springCloudVersion = '2023.0.1' // Use appropriate version compatible with your Spring Boot
        springCloudAlibabaVersion = '2023.0.1.0' // Use appropriate version compatible with your Spring Boot
    }
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public/' }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    group = 'com.heslin.postopia'
    version = '0.0.1-SNAPSHOT'
    repositories {
        mavenCentral()
        //maven { url 'https://maven.aliyun.com/repository/public/' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    bootJar {
        archiveFileName = "${project.name}.jar"
    }

    if (it.name.endsWith("-service")) {
        dependencies {
            developmentOnly 'org.springframework.boot:spring-boot-devtools'
            implementation 'org.springframework.boot:spring-boot-starter-web'
            implementation 'jakarta.persistence:jakarta.persistence-api'
            // lombok
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'
            implementation 'org.springframework.kafka:spring-kafka'
            implementation 'org.springframework.cloud:spring-cloud-starter'
            implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
            implementation 'io.github.openfeign:feign-okhttp'
        }
        if (it.name != "search-service") {
            dependencies {
                // postgres
                implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
                runtimeOnly 'org.postgresql:postgresql'
            }
        }
    }

    if (it.name != "common" && it.name != "jwt") {
        dependencies {
            implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
            implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery'
            implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
            implementation project(':common')
        }
    }

    dependencies {
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }
}









